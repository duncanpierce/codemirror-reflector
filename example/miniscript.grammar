// https://lezer.codemirror.net/docs/guide/

@external prop scope from "../src/props"
@external prop definition from "../src/props"
@external prop use from "../src/props"

@skip { space | Comment }

@tokens {
    space { $[ \t\n\r]+ }
    Comment { "#" ![\n]* }
    identifier { $[a-zA-Z]+ }
    Number { $[0-9]+ }
    String { '"' ![\n"] '"' }
}

@precedence {
    neg @left,
    mul @left,
    add @left,
    cmp @left
}

list<k> { "(" (k ("," k)*)? ")" }
Function { @specialize<identifier,"func"> }
Var { @specialize<identifier,"var"> }
Return { @specialize<identifier,"return"> }

@top Program[scope="namespaces:function variable"] { declaration* }

declaration {
    FunctionDeclaration |
    (GlobalVariableDeclaration ";")
}

GlobalVariableDeclaration { Var GlobalVariableDefinition }
VariableDeclaration { Var LocalVariableDefinition }
FunctionDeclaration[scope="namespaces:variable"] { Function FunctionDefinition FormalParameters "{" FunctionBody "}" }
FunctionBody { statement* }
statement { (VariableDeclaration | Assignment | FunctionCall | ReturnStatement) ";" }

FormalParameters { list<LocalVariableDefinition> }
ActualParameters { list<Expression> }
ReturnStatement { Return Expression }
Assignment { VariableUse "=" Expression }

Expression {
    Number |
    String |
    VariableUse |
    Unary |
    Binary |
    SubExpression |
    FunctionCall
}

Unary {
    !neg "-" Expression
}

Binary {
    Expression !mul "*" Expression |
    Expression !mul "/" Expression |
    Expression !add "+" Expression |
    Expression !add "-" Expression | 
    Expression !cmp ("<" | ">" | "=" | "<>" | "<=" | ">=") Expression
}

SubExpression { "(" Expression ")" }

FunctionCall { FunctionUse ActualParameters }

FunctionDefinition[definition="rules:unique,namespace:function"] { identifier }
GlobalVariableDefinition[definition="rules:unique,namespace:variable"] { identifier }
ParameterDefinition[definition="rules:unique,namespace:variable"] { identifier }
LocalVariableDefinition[definition="rules:following,namespace:variable"] { identifier }
VariableUse[use="namespace:variable"] { identifier }
FunctionUse[use="namespace:function"] { identifier }