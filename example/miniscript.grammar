// https://lezer.codemirror.net/docs/guide/

// Import props needed to define scopes, definitions and uses
@external prop scope from "../src/props"
@external prop definition from "../src/props"
@external prop use from "../src/props"

// Mark each node which defines a new scope - FunctionDeclaration has FunctionDefinition outside the new scope and its parameters inside
@top Program[scope] { declaration* }
FunctionDeclaration { Function FunctionDefinition FunctionScope[scope] { FormalParameters "{" FunctionBody "}" } }

// Mark each definition of a function or variable
GlobalVariableDefinition[definition] { identifier }
FunctionDefinition[definition] { identifier }
ParameterDefinition[definition] { identifier }
LocalVariableDefinition[definition] { identifier }

// Mark each use of a function or variable
VariableUse[use] { identifier }
FunctionUse[use] { identifier }

// End of extra things for Reflector - everything below is ordinary grammar

@skip { space | Comment }

@tokens {
    space { $[ \t\n\r]+ }
    Comment { "#" ![\n]* }
    identifier { $[a-zA-Z]+ }
    Number { $[0-9]+ }
    String { '"' ![\n"] '"' }
}

@precedence {
    neg @left,
    mul @left,
    add @left,
    cmp @left
}

list<k> { "(" (k ("," k)*)? ")" }
Function { @specialize<identifier,"func"> }
Var { @specialize<identifier,"var"> }
Return { @specialize<identifier,"return"> }

declaration {
    FunctionDeclaration |
    (GlobalVariableDeclaration ";")
}

GlobalVariableDeclaration { Var GlobalVariableDefinition }
VariableDeclaration { Var LocalVariableDefinition }
FunctionBody { statement* }
statement { (VariableDeclaration | Assignment | FunctionCall | ReturnStatement) ";" }

FormalParameters { list<ParameterDefinition> }
ActualParameters { list<Expression> }
ReturnStatement { Return Expression }
Assignment { VariableUse "=" Expression }

Expression {
    Number |
    String |
    VariableUse |
    Unary |
    Binary |
    SubExpression |
    FunctionCall
}

Unary {
    !neg "-" Expression
}

Binary {
    Expression !mul "*" Expression |
    Expression !mul "/" Expression |
    Expression !add "+" Expression |
    Expression !add "-" Expression | 
    Expression !cmp ("<" | ">" | "=" | "<>" | "<=" | ">=") Expression
}

SubExpression { "(" Expression ")" }

FunctionCall { FunctionUse ActualParameters }
